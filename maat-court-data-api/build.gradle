plugins {
    id 'java'
    id 'jacoco'
    id 'org.springframework.boot' version "$springframeworkBootVersion"
    id 'io.spring.dependency-management' version "$springDependencyManagementVersion"
    id 'com.javiersc.semver.project' version "$semverVersion"
    id 'com.gorylenko.gradle-git-properties' version "$gitPropertiesVersion"
    id 'info.solidsoft.pitest' version "$pitestVersion"
}

group = 'gov.uk'
description = 'This is a Facade application to the existing LAA legacy Applications MAAT/MLRA.'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of("$javaVersion")
    }
}

sourceSets {
    testSqsIntegration {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    testSqsIntegrationImplementation.extendsFrom implementation
}

gitProperties {
    failOnNoGitDirectory = false
    dotGitDirectory = file("${project.rootDir}/../.git")
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://s01.oss.sonatype.org/content/repositories/snapshots/"
    }
}

dependencies {

    implementation platform("io.sentry:sentry-bom:$sentryBomVersion")

    implementation "uk.gov.justice.service.laa-crime:crime-commons-spring-boot-starter-rest-client:$commonsRestClient"
    implementation "uk.gov.justice.service.laa-crime:crime-commons-classes:$crimeCommonsClasses"
    implementation "uk.gov.justice.service.laa-crime:crime-commons-mod-schemas:$commonsModSchemas"

    implementation("io.sentry:sentry-spring-boot-starter-jakarta")
    implementation("io.sentry:sentry-logback")

    //Required for Logback configuration due to a dependency having their own implementation of SAXParserFactory
    implementation "xerces:xercesImpl:$xercesImplVersion"

    // Spring
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-webflux"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-aop"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-resource-server"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-client"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-graphql"

    implementation "org.springframework:spring-jms"
    implementation "com.amazonaws:amazon-sqs-java-messaging-lib:$amazonSQSVersion"
    implementation "io.awspring.cloud:spring-cloud-aws-starter-sqs:$springAwsCloudVersion"

    implementation "software.amazon.awssdk:sts:$awsStsVersion"

    implementation "io.projectreactor.netty:reactor-netty"
    implementation "commons-io:commons-io:$commonsIOVersion"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:$springdocVersion"

    annotationProcessor 'org.projectlombok:lombok'
    implementation "org.mapstruct:mapstruct:$mapstructVersion"
    annotationProcessor "org.mapstruct:mapstruct-processor:$mapstructVersion"
    //Required for lombok version > 1.18.16
    annotationProcessor "org.projectlombok:lombok-mapstruct-binding:$lombokMapStrutBinding"

    implementation "io.micrometer:micrometer-registry-cloudwatch2:$micrometerCloudWatchVersion"
    implementation "io.micrometer:micrometer-registry-prometheus"

    // DB Dependencies
    implementation "com.oracle.database.jdbc:ojdbc8:$ojdbc8Version"
    implementation "com.oracle.database.xml:xmlparserv2:$oracleXMLParserVersion"
    implementation "com.oracle.database.xml:xdb:$oracleXMLParserVersion"

    annotationProcessor "org.hibernate:hibernate-jpamodelgen:$hibernateJpaModelGen"
    implementation "com.graphql-java-kickstart:graphql-java-tools:$graphqlJavaTools"

    implementation "com.google.code.gson:gson:$gsonVersion"
    implementation "org.hibernate.orm:hibernate-core:$hibernateCore"

    // Test Only
    testImplementation "com.h2database:h2"
    testImplementation "org.mockito:mockito-inline:$mockitoInlineVersion"
    testImplementation "org.hamcrest:hamcrest-all:$harmcrestVersion"
    testImplementation "org.easytesting:fest-assert:$festVersion"
    testCompileOnly "org.projectlombok:lombok"
    testAnnotationProcessor "org.projectlombok:lombok"
    testImplementation "org.springframework.cloud:spring-cloud-starter-contract-stub-runner:$springCloudStubRunnerVersion"

    // Only required for SQS Integration test
    testSqsIntegrationImplementation "org.awaitility:awaitility:$awaitilityVersion"
    testSqsIntegrationImplementation "com.amazonaws:aws-java-sdk:$awsJavaSdkVersion"
    testSqsIntegrationImplementation "com.amazonaws:aws-java-sdk-sqs:$awsJavaSdkVersion"
    testSqsIntegrationImplementation "org.testcontainers:junit-jupiter:$testcontainersJunitJupiterVersion"
    testSqsIntegrationImplementation "org.testcontainers:localstack:$testcontainersLocalstackVersion"
    testSqsIntegrationImplementation "cloud.localstack:localstack-utils:$cloudLocalstackUtilsVersion"
    testSqsIntegrationImplementation 'com.h2database:h2'
    testSqsIntegrationImplementation "org.springframework.cloud:spring-cloud-starter-contract-stub-runner:$springContractStubRunnerVersion"
}

jacoco {
    toolVersion = "0.8.13"
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "**/entity/**",
                    "**/dto/**",
                    "**/config/**",
                    "**/dao/**"
            ])
        }))
    }
}

task testSqsIntegration(type: Test) {
    useJUnitPlatform()
    testClassesDirs = sourceSets.testSqsIntegration.output.classesDirs
    classpath = sourceSets.testSqsIntegration.runtimeClasspath
}

check.dependsOn testSqsIntegration

pitest {
    targetClasses = ['gov.uk.courtdata.link.*', 'gov.uk.courtdata.unlink.*', 'gov.uk.courtdata.laastatus.*', 'gov.uk.courtdata.hearing.*', 'gov.uk.courtdata.dao.*']
    excludedClasses = ['gov.uk.courtdata.hearing.dto.*', 'gov.uk.courtdata.hearing.mapper.*', 'gov.uk.courtdata.laastatus.client.*', 'gov.uk.courtdata.dao.convertor.*', 'gov.uk.courtdata.dao.oracle.*']
    threads = 5
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
    mutationThreshold = 60
}